buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.gradleup.shadow:shadow-gradle-plugin:9.0.0-beta4'
    }
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'com.gradleup.shadow'
apply plugin: 'maven-publish'

group = 'fr.democraft.rcm'
version = module_version

repositories {
    mavenCentral()
    maven { url = "https://maven.mrnavastar.me/snapshots/" }
    maven { url = "https://maven.mrnavastar.me/releases/" }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
}

dependencies {
    compileOnly 'group.aelysium.rustyconnector:core:0.9.1'
    compileOnly 'net.kyori:adventure-api:4.17.0'
    compileOnly 'org.spigotmc:spigot-api:1.21-R0.1-SNAPSHOT'
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withJavadocJar()
    withSourcesJar()
}

compileJava {
    options.encoding = "UTF-8"
}

javadoc {
    options.encoding = 'UTF-8'
}

shadowJar {
    archiveBaseName.set(archives_base_name)
    archiveClassifier.set('')
    mergeServiceFiles()
    relocate 'org.reflections', 'group.aelysium.rustyconnector.shaded.org.reflections'
}

tasks.named('jar').configure {
    enabled = false
}
tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xdoclint:none']
}
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

jar.finalizedBy(shadowJar)

publishing {
    repositories {
        maven {
            name = 'Reposilite'
            url = 'https://java.funa.dev/releases'

            credentials {
                // Use environment variables for username and password
                username = System.getenv("REPOSILITE_USERNAME")
                password = System.getenv("REPOSILITE_PASSWORD")
            }
        }
    }

    publications {
        maven(MavenPublication) {
            artifact shadowJar
            from components.java
            groupId = 'fr.democraft.rcm'
            artifactId = 'smart'
            version = module_version
        }
    }
}